from sys import platform

libname = 'Distillate';

IncludePath = ['include/']
LibsPath = []   
Libs = []

CommonFlags = ' -Wall -pedantic'

debug    = ARGUMENTS.get('debug', 'true')
linkage  = ARGUMENTS.get('linkage', 'static')

video    = ARGUMENTS.get('video',  'sdl')
render   = ARGUMENTS.get('render', 'hw')
png      = ARGUMENTS.get('png', 0)

libenv = Environment( CPPFLAGS = CommonFlags)

if debug == 'true':
    libenv.Append(CPPFLAGS = ' -g -ggdb')
    libenv.Append(CPPDEFINES = ['DEBUG'])

if video == 'sdl':
    libenv.Append(CPPDEFINES = ['SDL_VIDEO'])
    Libs.append('SDL')
    Libs.append('SDL_image')
    Libs.append('SDL_ttf')
    Libs.append('SDL_mixer')
    if render == 'hw':
        libenv.Append(CPPDEFINES = ['HW_RENDER'])
        Libs.append('GL')
        Libs.append('GLU')
    if render == 'sw':
        libenv.Append(CPPDEFINES = ['SW_RENDER'])
        Libs.append('SDL_gfx')

if video == 'x11':
    if render == 'hw':
        libenv.Append(CPPDEFINES = ['X11_VIDEO'])
        Libs.append('GL')
        Libs.append('GLU')
        if platform == 'linux2':
            Libs.append('X11')
            Libs.append('Xxf86vm')
            Libs.append('png')
            LibsPath.append('/usr/lib/X11')
    elif render == 'sw':
        print "SW RENDER DISABLE FOR X11"
        Exit(-1)

if int(png):
    libenv.Append(CPPDEFINES = ['PNG_IMAGE_LOADER'])
    Libs.append('png')

files = Split("""
Emitter.cpp
Game.cpp
Globals.cpp
Group.cpp
Keyboard.cpp
Mouse.cpp
Object.cpp
QuadTree.cpp
ResourceManager.cpp
Sound.cpp
SoundResource.cpp
Sprite.cpp
State.cpp
Text.cpp
TextureResource.cpp
TTFResource.cpp
Utils.cpp
""");

if linkage == 'static':
    lib = libenv.StaticLibrary(target = "../%s" % libname, source = files, CPPPATH = IncludePath, LIBS = Libs, LIBPATH = LibsPath)
elif linkage == 'shared':
    lib = libenv.SharedLibrary(target = "../%s" % libname, source = files, CPPPATH = IncludePath, LIBS = Libs, LIBPATH = LibsPath)

